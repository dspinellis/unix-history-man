#!/usr/bin/env/perl
#
# Visualize all the elements as an SVG file
#

use strict;
use warnings;

# All documented elements
my %documented_all;
# What's documented for each release
my %documented_release;
my @releases;

open(my $T, '<', '../data/timeline') || die;
while (<$T>) {
	my ($release) = split;
	push(@releases, $release);
	open(my $DOC, '<', "../data/$release") || die;
	while (<$DOC>) {
		my ($section, $name, $url) = split;
		my $ref = "$section\t$name";
		$documented_all{$ref} = 1;
		$documented_release{$release}{$ref} = 1;
	}
}

my $n_features = keys %documented_all;
my $n_releases = $#releases + 1;
my $width = int(($n_features + 1) * 16 / 9);

print qq[<?xml version="1.0"?>
<!DOCTYPE svg PUBLIC '-//W3C//DTD SVG 1.0//EN'
          'http://www.w3.org/TR/2001/REC-SVG-20010904/DTD/svg10.dtd'>
<svg xmlns:xlink="http://www.w3.org/1999/xlink" style="fill-opacity:1; color-rendering:auto; color-interpolation:auto; text-rendering:auto; stroke:black; stroke-linecap:square; stroke-miterlimit:10; shape-rendering:auto; stroke-opacity:1; fill:black; stroke-dasharray:none; font-weight:normal; stroke-width:1; font-family:'Dialog'; font-style:normal; stroke-linejoin:miter; font-size:12px; stroke-dashoffset:0; image-rendering:auto;" width="$width" height="$n_features" xmlns="http://www.w3.org/2000/svg">
<!--Generated by visualize.pl-->
<defs id="genericDefs" />
<g >
  <g style="stroke-linecap:round; fill:white; stroke:white;" >
    <rect x="0" width="$width" height="$n_features" y="0" style="stroke:none;" />
  </g>
  <g style="stroke-linecap:round; fill:blue; stroke:blue;">
];

my $y = 0;
for my $feature (sort keys %documented_all) {
	print "<!-- $feature -->\n";
	my $x = 0;
	for my $release (@releases) {
		$x++;
		if ($documented_release{$release}{$feature}) {
			my $x1 = int(($x / $n_releases) * ($width - 1));
			my $x2 = int((($x + 1) / $n_releases) * ($width - 1));
			print qq[<line x1="$x1" y1="$y" x2="$x2" y2="$y" style="fill:none;" />\n];
		}
	}
	print "\n";
	$y++;
}

print q[
  </g>
</g>
</svg>
];
